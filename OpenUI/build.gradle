import org.gradle.internal.os.OperatingSystem

apply plugin: 'java-library'
apply plugin: 'maven-publish'

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
			? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
			: "natives-windows-x86"
		break
}

version = '1.3.2.2-alpha'

repositories {
	mavenCentral()
	maven {
		url 'https://maven.pkg.github.com/m-marvin/library-simplelogging'
		credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
	}
	maven {
		url 'https://maven.pkg.github.com/m-marvin/library-unifiedvectors'
		credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
	}
	maven {
		url 'https://maven.pkg.github.com/m-marvin/library-graphicsframework'
		credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
	}
	mavenLocal()
}

java {
	sourceCompatibility = "1.17"
	targetCompatibility = "1.17"
    withSourcesJar()
}

dependencies {
	
	// LWJGL
	runtimeOnly "org.lwjgl:lwjgl:3.3.3:$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw:3.3.3:$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl:3.3.3:$lwjglNatives"
	
	// RenderEngine
	api 'de.m_marvin.gframe:gframe:1.4.3.1'
	
	// ArchiveUtility
	api 'de.m_marvin.archiveutility:archiveutility:2.1'
	
	// JBullet (Voxel Engine)
	testImplementation "cz.advel.jbullet:jbullet:20101010-1"
	
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/m-marvin/library-openui")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'de.m_marvin.openui'
        	artifactId = "openui"
        
			from components.java
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Vendor-Id': 'de.m_marvin'
    }
}
